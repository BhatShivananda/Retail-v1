apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'com.palantir.jacoco-coverage'
apply plugin: 'org.springframework.boot'
apply plugin: 'project-report'

buildscript {
    ext {
        springBootVersion = '1.5.11.RELEASE'
        springBootGroup = 'org.springframework.boot'
    }
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("${springBootGroup}:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "gradle.plugin.com.palantir:jacoco-coverage:0.4.0"
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    all*.exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    all*.exclude group: 'ch.qos.logback', module: 'logback-classic'
    all*.exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

dependencies {
    def springVersion = '4.3.9.RELEASE'
    def springGroup = 'org.springframework'
    def hystrixVersion = '1.5.6'
    def springCloudVersion = '1.2.6.RELEASE'
    def log4jVersion = '2.11.1'

    compile(group: springBootGroup, name: 'spring-boot-starter-web', version: springBootVersion)
    compile(group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.9.3')

    compile(group: 'com.ryantenney.metrics', name: 'metrics-spring', version: '3.1.3')

    compile(group: 'io.springfox', name: 'springfox-swagger2', version: '2.6.1')
    // Use this until our cassandra versions support guava 20.0 or springfox 3.0 comes out
    compile(group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.6.1')

    // Lombok
    compileOnly "org.projectlombok:lombok:1.16.16"

    // Spring
    compile(group: 'org.springframework.cloud', name: 'spring-cloud-starter-feign', version: springCloudVersion)
    compile(group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix', version: springCloudVersion)

    // Hystrix
    compile(group: 'com.netflix.hystrix', name: 'hystrix-javanica', version: hystrixVersion)
    compile(group: 'com.netflix.hystrix', name: 'hystrix-servo-metrics-publisher', version: hystrixVersion)

    // Apache commons
    compile(group: 'org.apache.commons', name: 'commons-lang3', version: '3.3.2')
    compile(group: 'org.apache.commons', name: 'commons-collections4', version: '4.1')

    //Logging
    compile(group: 'org.slf4j', name: 'slf4j-api', version: '1.7.8')
    compile(group: 'commons-logging', name: 'commons-logging', version: '1.2')
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-1.2-api', version: log4jVersion
    compile group: 'org.apache.logging.log4j', name: 'log4j-jcl', version: log4jVersion

    //Cassandra
    compile(group: 'com.datastax.cassandra', name: 'cassandra-driver-core', version: '3.3.0')
    compile(group: 'com.datastax.cassandra', name: 'cassandra-driver-mapping', version: '3.3.0')

    testCompile(group: 'com.google.code.gson', name: 'gson', version: '2.6.2')

    testCompile(group: springGroup, name: 'spring-test', version: springVersion)
    testCompile(group: 'junit', name: 'junit', version: '4.12')
    testCompile(group: 'org.mockito', name: 'mockito-all', version: '1.10.19')
    testCompile(group: 'org.apache.cassandra', name: 'cassandra-all', version: '2.2.9')
    testCompile(group: 'org.cassandraunit', name: 'cassandra-unit-spring', version: '3.1.3.2') {
        exclude(module: "slf4j-log4j12")
        exclude(group: 'org.apache.cassandra', module: 'cassandra-all')
        exclude(group: 'org.apache.cassandra', module: 'cassandra-thrift')
    }
}

jacocoCoverage {
    reportThreshold 1.0
    packageThreshold 1.0
    classThreshold 1.0
}
jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    '**/*RetailMainApp**',
                    'com/app/retail/entities/**',
                    'com/app/retail/model/**',
                    'com/app/retail/config/**',
                    'com/app/retail/error/**'
            ])
        })
    }
    reports {
        xml.enabled false
        csv.enabled false
    }
}